plugins { id "io.spring.dependency-management" version "1.0.5.RELEASE" }

allprojects{
    group 'nl.oscar.dpi'
    version '0.0.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        //compile('org.springframework.boot:spring-boot-starter')

        testCompile group: 'junit', name: 'junit', version: '4.12'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    }

    test {
        reports.getJunitXml().setDestination(file("build/test-reports"))
    }

    sourceSets {
        integrationTest {
            java {
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
            }
        }
    }

    configurations {
        integrationTestCompile.extendsFrom testCompile
        integrationTestRuntime.extendsFrom testRuntime
    }

    task integrationTest(type: Test) {
        setTestClassesDirs(sourceSets.integrationTest.getOutput().getClassesDirs())
        setClasspath(sourceSets.integrationTest.getRuntimeClasspath())

        reports.getJunitXml().setDestination(file("build/integration-test-reports"))
    }

    //check.dependsOn integrationTest
    integrationTest.mustRunAfter test
}
